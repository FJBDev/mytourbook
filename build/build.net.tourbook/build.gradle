plugins {

   id 'com.github.dkorotych.gradle-maven-exec' version '2.2.1'
    id 'org.sonarqube' version '3.3'
}

// get MT version and remove quotes
def _mtVersionRaw                = project(':mytourbook').ext.myTourbookVersion      // '18.2.0'
def _mtVersion                   = _mtVersionRaw.replaceAll('\'', '')                // 18.2.0

def _dirProductBuilt             = file('../build.net.tourbook.product/target/products')
def _dirProductArchive           = file('../../../../mytourbook-PRODUCT/' + _mtVersion)
def _dirProductReleaseNotes      = file('../../info/release-notes')
def _pathProductArchive          = _dirProductArchive.absolutePath
                                 
def _dirWinInnoSetup             = file('../../INSTALL-UPDATES/installer-net.tourbook/win-Inno.Setup')
def _pathInnoSetupCompilerApp    = file('c:/Program Files (x86)/Inno Setup 6/iscc.exe').absolutePath

gradle.taskGraph.beforeTask { Task task ->

    // show a task indicator
   println '---------------------------------------------------------------------------------------'
   println ''
}

gradle.taskGraph.afterTask { Task task ->

    // show more space
   println ''
   println ''
}

defaultTasks 'mt_0_Build'

task mt_0_Build          
mt_0_Build.dependsOn( 

   'mt_10_CreateBuildFiles',
   'mt_20_BuildProduct',
   'mt_30_CopyProduct',
   'mt_40_ExtractWinFiles'
)

sonarqube {
  properties {
    property "sonar.projectKey", "FJBDev_mytourbook"
    property "sonar.organization", "fjbdev"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.projectBaseDir", "/bundles"
    property "sonar.exclusions", "build/**/*,bundles/net.tourbook.chart/src/net/tourbook/chart/ChartComponentGraph.java,bundles/net.tourbook.ext.jars/**/*"
    property "sonar.java.binaries", "D:\\a\\mytourbook-BUILD-autocreated\\core"
  }
}

/*
* 1. Copy all MT files into a temp folder, it is using task/target 'create-build-files' from the Ant script
*/
task mt_10_CreateBuildFiles {

   // import Ant build, every target will be a task
   ant.importBuild 'CREATE_MT_BuildFiles.xml'

   dependsOn('create-build-files')
}


/*
* 2. Build product with maven/tycho
*/
task mt_20_BuildProduct {

   doLast {

      //run: mvn verify -P build-release

      def buildDir = file('../..')

      mavenexec {

         workingDir  buildDir
         goals       'verify'
         
         // repos can be used offline when this option "offline true" is set, 
         // by default it is disabled 
//       offline     true

         options {
            activateProfiles   = ['build-release']
         }
      }
   }
}


/*
* Copy generated product file into product archive folder
*/
task mt_30_CopyProduct {

   doLast {

//    From:   C:\DAT\MT\mytourbook\build\build.net.tourbook
//    Into:   C:\DAT\MT\mytourbook\build\build.net.tourbook.product\target\products

      println '\tFrom ' + _dirProductBuilt       
      println '\tInto ' + _dirProductArchive

      // copy into archive folder
      copy {

         from (_dirProductBuilt) {
            include '*' + _mtVersion + '*'
         }

         into _dirProductArchive
      }
   }
}


/*
 Extract win archives in separate dirs that the exe files can be compiled
*/
task mt_40_ExtractWinFiles {

   doLast {

      println 'Extracting from ' + _dirProductArchive

      file(_dirProductArchive).eachFileMatch(~/.*win.*.zip/) { winZipFile->

         def winZipFileName = winZipFile.name
         def winZipName = winZipFileName.take(winZipFileName.lastIndexOf('.'))

         def outputDir = file(_pathProductArchive + '/' + winZipName)

         println '\t' + outputDir
         
         copy {
            from zipTree(winZipFile)
            into outputDir
         }
      }
   }
}
